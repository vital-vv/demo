plugins {
  id 'com.github.davidmc24.gradle.plugin.avro'
  id 'org.springframework.boot'
  id 'io.spring.dependency-management'
  id 'com.palantir.docker'
  id 'jacoco'
  id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

sourceSets {
  main.resources.srcDirs += "src/main/avro"
}

repositories {
  mavenCentral()
  maven { url "https://packages.confluent.io/maven/" }
}

dependencies {
  annotationProcessor(
      'org.projectlombok:lombok',
      "org.mapstruct:mapstruct-processor:$mapstructVersion"
  )
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation "org.springframework.cloud:spring-cloud-stream"
  implementation "org.springframework.cloud:spring-cloud-stream-binder-kafka"
  implementation 'org.springframework.statemachine:spring-statemachine-starter'
  implementation 'org.springframework.statemachine:spring-statemachine-data-jpa'
  implementation "io.confluent:kafka-avro-serializer:$confluentVersion"
  implementation "org.postgresql:postgresql:$postgresqlVersion"
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.statemachine:spring-statemachine-test'
  testImplementation 'com.h2database:h2'
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    mavenBom "org.springframework.statemachine:spring-statemachine-bom:$springStatemachineVersion"
    mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
  }
}

test {
  useJUnitPlatform()
}

jacocoTestReport {
  dependsOn test
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        minimum = 0.8
      }
    }
  }
}

docker {
  name 'demo/employee-processor'
  dockerfile rootProject.file('docker/Dockerfile')
  files tasks.bootJar.outputs.files.singleFile
  buildArgs JAR_FILE: bootJar.archiveFileName.get()
  dependsOn bootJar
}
